plugins {
    id 'java'
    id 'maven-publish'
    id 'net.neoforged.gradleutils' version '2.+'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'net.neoforged'

base {
    archivesName = 'DiffPatch'
}

version = gradleutils.getTagOffsetVersion()
println('Version: ' + version + ' Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

ext {
    MIN_CHANGELOG_TAG = '2.0'
}

repositories {
    mavenCentral()
}

sourceSets {
    create('gradle')
    main
}

configurations {
    shadow
    implementation.extendsFrom shadow
    gradleImplementation.extendsFrom implementation
}

dependencies {
    shadow 'it.unimi.dsi:fastutil:8.3.1'
    shadow 'org.apache.commons:commons-lang3:3.9'
    shadow 'org.apache.commons:commons-compress:1.18'
    shadow 'org.tukaani:xz:1.8'
    shadow 'net.sf.jopt-simple:jopt-simple:5.0.4'
    gradleImplementation sourceSets.main.output
    gradleCompileOnly gradleApi()

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}

test {
    useJUnitPlatform()
}

def commonManifest = {
    attributes 'Main-Class': 'codechicken.diffpatch.DiffPatch'
}

jar {
    manifest commonManifest
    from file("LICENSE.txt")
    from sourceSets.gradle.output
}

shadowJar {
    minimize()
    manifest commonManifest
    configurations = [project.configurations.shadow]

    //Include license, exclude java9 and maven things.
    from file("LICENSE.txt")
    from sourceSets.gradle.output
    exclude 'META-INF/maven/**'
    exclude 'module-info.class'

    //Relocate all our dependencies into a repack package.
    relocate 'it.unimi', 'codechicken.repack.it.unimi'
    relocate 'org.apache', 'codechicken.repack.org.apache'
    relocate 'org.tukaani', 'codechicken.repack.org.tukaani'
    relocate 'joptsimple', 'codechicken.repack.joptsimple'
    //Transform joptsimple properties files.
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer) {
        paths << "joptsimple/ExceptionMessages.properties"
        paths << "joptsimple/HelpFormatterMessages.properties"
        keyTransformer = { key ->
            key.replaceAll('^(joptsimple\\..*)$', 'codechicken.repack.$1')
        }
    }
}

java {
    withSourcesJar()
}

publishing {
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
    publications {
        DiffPatch(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components['java']

            pom {
                name = archivesBaseName
                description = archivesBaseName
                //The publish plugin doesnt like GString's here apparently..
                url = "https://github.com/neoforged/${base.archivesName.get()}".toString()
                scm {
                    url = "https://github.com/neoforged/${base.archivesName.get()}".toString()
                    connection = "scm:git:git://github.com/neoforged/${base.archivesName.get()}.git".toString()
                    connection = "scm:git:git@github.com:neoforged/${base.archivesName.get()}.git".toString()
                }

                issueManagement {
                    system = 'github'
                    url = "https://github.com/neoforged/${base.archivesName.get()}/issues".toString()
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://raw.githubusercontent.com/neoforged/DiffPatch/master/LICENSE.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'covers1624'
                        name = 'covers1624'
                    }
                }
                developers {
                    developer {
                        id = 'ChickenBones'
                        name = 'Chicken Bones'
                    }
                }
            }
        }
    }
}

changelog {
    fromTag MIN_CHANGELOG_TAG
}